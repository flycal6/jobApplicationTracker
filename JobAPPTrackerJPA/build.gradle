/*JPA Rest backend gradle build template*/

apply plugin: 'java'
apply plugin: 'eclipse-wtp'

/*
name your project's dependency group and give it an initial version.
the 'artifactId' is assigned automatically based on the project's name.
*/
group = 'com.jobapptracker'
version = '0.0.1-SNAPSHOT'

/*
 set the version of hibernate to use to keep your dependencies DRY and
 consistent across artifactIds
*/
ext {
  hibernateVersion = "5.2.1.Final"
}

// JDK versioning for compilation and generated bytecode
sourceCompatibility = 1.8
targetCompatibility = 1.8

// tell the 'GenerateEclipseClasspath' task how to generate the .classpath file
eclipse {
  classpath {
    downloadSources = true
  }
}

// explicitly configure the sourced files of the application for compilation
sourceSets {
  main {
    java.srcDirs 'src'
    resources.srcDirs 'src/resources'
  }
  test {
    java {
      srcDir 'test'
    }
    resources {
      srcDir 'src'
    }
  }
}

// set query location for dependencies
repositories {
  mavenCentral()
}

// define project specific dependencies
dependencies {
  // mysql
  compile "mysql:mysql-connector-java:5.1.39"
  // sql queries debugging
  compile "log4j:log4j:1.2.17"
  // hibernate/jpa
  compile "org.hibernate:hibernate-core:$hibernateVersion"
  compile "org.hibernate:hibernate-c3p0:$hibernateVersion"
  // jackson jars for json/rest
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.5'
  
  // junit testing
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-junit:2.0.0.0'
}